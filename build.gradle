buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.comsysto.gradle'
version = '0.0.1-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    compile 'me.champeau.gradle:jmh-gradle-plugin:0.2.0'
}

task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'crolabefra-java'
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            pom.withXml {
                def Node pomRoot = asNode()
                pomRoot.appendNode('name', 'CroLaBeFra Gradle Plugin for Java')
                pomRoot.appendNode('url', 'https://github.com/comsysto/CroLaBeFra')

                def developer = pomRoot.appendNode('developers').appendNode('developer');
                developer.appendNode('id', 'bensteinert')
                developer.appendNode('name', 'Benjamin Steinert')
                developer.appendNode('organization', 'comSysto')
                developer.appendNode('organizationUrl', 'http://comsysto.com')
            }
        }
    }
}

bintray {
    user = System.getProperty('bintrayUsername')
    key = System.getProperty('bintrayKey')
    dryRun = true
    publish = false

    publications = ['mavenJava']
    pkg {
        repo = 'crolabefra'
        name = 'crolabefra-java'
        desc = 'CroLaBeFra Gradle Plugin for Java Benchmarks with JMH'
        licenses = ['Apache-2.0']
        labels = ['jmh','gradle', 'crolabefra']
    }
}


